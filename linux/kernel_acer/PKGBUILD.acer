pkgbase=linux
pkgname=$pkgbase
pkgver=
pkgrel=
pkgdesc="The Linux kernel and modules"
arch=(x86_64)
license=(GPL2)

provides=(VIRTUALBOX-GUEST-MODULES WIREGUARD-MODULE)
replaces=(virtualbox-guest-modules-arch wireguard-arch)
depends=(coreutils kmod initramfs)
optdepends=('crda: to set the correct wireless channels of your country'
            'linux-firmware: firmware images needed for some devices')
makedepends=(
  bc kmod libelf pahole cpio perl tar xz
  xmlto python-sphinx python-sphinx_rtd_theme graphviz imagemagick
  git
)

source=(config)
sha256sums=('SKIP')
options=('!strip')

export KBUILD_BUILD_HOST=archlinux
export KBUILD_BUILD_USER=$pkgbase
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

KernelTag="v${pkgver%.*}-${pkgver##*.}"
KernelSrcDir="archlinux-linux"

prepare()
{
    cd "$srcdir"
    if [[ -d "$KernelSrcDir" ]]
    then
        echo "Fetching source code for latest kernel version..."
        cd "$KernelSrcDir"
        git fetch --depth 1 origin "$KernelTag"
        git checkout -f FETCH_HEAD
    else
        echo "Cloning the kernel source code repository..."
        git clone -b "$KernelTag" --depth 1 'https://github.com/archlinux/linux.git' "$KernelSrcDir"
        cd "$KernelSrcDir"
    fi

    # NOTE(nox): SA5-271 patch
    sed -i 's/Switch SA5-271/SA5-271/' drivers/ata/ahci.c

    # NOTE(nox): From the original PKGBUILD ========================================================
    echo "Setting version..."
    scripts/setlocalversion --save-scmversion
    echo "-$pkgrel" > localversion.10-pkgrel
    echo "${pkgbase#linux}" > localversion.20-pkgname

    echo "Setting config..."
    cp ../config .config
    make olddefconfig
    diff -u ../config .config || :

    make -s kernelrelease > version
    echo "Prepared $pkgbase version $(<version)"
    # ================================================================================================
}

build()
{
    cd "$srcdir/$KernelSrcDir"
    make all
}

package()
{
    cd "$srcdir/$KernelSrcDir"

    # NOTE(nox): From the original PKGBUILD ========================================================
    local kernver="$(<version)"
    local modulesdir="$pkgdir/usr/lib/modules/$kernver"

    echo "Installing boot image..."
    # systemd expects to find the kernel here to allow hibernation
    # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
    install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

    # Used by mkinitcpio to name the kernel
    echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

    echo "Installing modules..."
    make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 modules_install

    # remove build and source links
    rm "$modulesdir"/{source,build}
    # ================================================================================================
}

# Local Variables:
# mode: sh
# End:
